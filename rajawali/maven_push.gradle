apply plugin: 'maven'
apply plugin: 'signing'

def getRepositoryUsername() {
    return hasProperty('SONATYPE_UID') ? SONATYPE_UID : ""
}

def getRepositoryPassword() {
    return hasProperty('SONATYPE_PASS') ? SONATYPE_PASS : ""
}

boolean isLocal() {
    return (getRepositoryUsername().isEmpty() || getRepositoryPassword().isEmpty())
}

boolean isRelease() {
    return VERSION_NAME.contains("SNAPSHOT") == false
}

static String executeCommand(String command) {
    return command.execute().text.trim()
}

boolean isTag() {
    return !executeCommand("git name-rev --name-only --tags HEAD").isEmpty()
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                VERSION_NAME = isTag() ? VERSION_NAME.replace("-SNAPSHOT", "") : VERSION_NAME

                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                pom.artifactId = POM_ARTIFACT_ID

                repository(url: isLocal() ? mavenLocal().url : "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                snapshotRepository(url: isLocal() ? mavenLocal().url : "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                            organization POM_DEVELOPER_ORGANIZATION
                            organizationUrl POM_DEVELOPER_ORGANIZATION_URL
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isRelease() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.sourceFiles
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
