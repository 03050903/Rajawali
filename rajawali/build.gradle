apply plugin: 'com.android.library'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME
status = 'debug'

static String executeCommand(String command) {
    return command.execute().text.trim()
}

static boolean isTag() {
    def travisIsTag = System.getenv("TRAVIS_TAG")
    println "TRAVIS TAG: " + travisIsTag
    return System.getenv("TRAVIS_TAG") != null
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            status = 'release'

            Properties properties = new Properties()
            def propsFile = project.rootProject.file('local.properties')
            if (propsFile.exists()) {
                properties.load(propsFile.newDataInputStream())
            }

            ext."signing.keyId" = System.getenv('SIGNING_KEY') != null ? System.getenv('SIGNING_KEY') : properties.getProperty('SIGNING_KEY')
            ext."signing.password" = System.getenv('SIGNING_KEY_PASSWORD') != null ? System.getenv('SIGNING_KEY_PASSWORD') : properties.getProperty('SIGNING_KEY_PASSWORD')
            ext."signing.secretKeyRingFile" = "./rajawali_secret.gpg"

            def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER')
            if (buildNumber == null) {
                buildNumber = properties.getProperty('TRAVIS_BUILD_NUMBER')
                if (buildNumber == null) throw new Exception("Build number was null! Failing!")
            }
            version = version.replaceAll("(0(?=-SNAPSHOT)|0\$)", buildNumber)

            if (isTag()) {
                println "This is a tag - removing SNAPSHOT suffix."
                version = VERSION_NAME.replace("-SNAPSHOT", "")
            }

            def branchName = System.getenv('TRAVIS_BRANCH') != null ? System.getenv('TRAVIS_BRANCH') : properties.getProperty('TRAVIS_BRANCH')
            if (branchName == null) throw new Exception("Branch name was null! Failing!")
            if (!branchName.equals('master') && !isTag()) {
                println "Branch is not master. Canceling upload of artifacts."
                project.uploadArchives.enabled = false
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
}

apply from: 'maven_push.gradle'
